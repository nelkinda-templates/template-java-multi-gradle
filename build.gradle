buildscript {
    repositories {
        mavenCentral()
        maven {
            url 'https://plugins.gradle.org/m2/'
        }
    }
    dependencies {
        classpath 'name.remal:gradle-plugins:1.0.208'
        classpath 'org.sonarsource.scanner.gradle:sonarqube-gradle-plugin:3.0'
        classpath 'info.solidsoft.gradle.pitest:gradle-pitest-plugin:1.5.1'
    }
}

plugins {
    id 'name.remal.check-updates' version '1.0.208'
    id 'io.spring.dependency-management' version '1.0.9.RELEASE'
}

allprojects {
    repositories {
        jcenter()
    }
}

subprojects {
    apply plugin: 'io.spring.dependency-management'

    version '0.0.1-SNAPSHOT'
    dependencyManagement {
        dependencies {
            dependency 'com.puppycrawl.tools:checkstyle:8.35'
            dependencySet(group: 'io.cucumber', version: '6.3.0') {
                entry 'cucumber-java'
                entry 'cucumber-junit'
                entry 'cucumber-junit-platform-engine'
            }
            dependency 'junit:junit:4.13'
            dependencySet(group: 'net.sourceforge.pmd', version: '6.26.0') {
                entry 'pmd'
                entry 'pmd-core'
                entry 'pmd-java'
            }
            dependencySet(group: 'org.junit.jupiter', version: '5.6.2') {
                entry 'junit-jupiter'
                entry 'junit-jupiter-api'
                entry 'junit-jupiter-engine'
                entry 'junit-jupiter-params'
            }
            dependencySet(group: 'org.junit.platform', version: '1.6.2') {
                entry 'junit-platform-commons'
                entry 'junit-platform-engine'
            }
            dependency 'org.junit.vintage:junit-vintage-engine:5.6.2'
            dependency 'org.mockito:mockito-core:3.4.4'
            dependency 'org.pitest:pitest-junit5-plugin:0.12'
            dependency 'org.pitest:pitest-command-line:1.5.2'
            dependency 'org.projectlombok:lombok:1.18.12'
        }
    }
}

configure(subprojects) {
    apply plugin: 'java'
    apply plugin: 'checkstyle'
    apply plugin: 'pmd'
    apply plugin: 'jacoco'
    apply plugin: 'name.remal.sonarlint'
    apply plugin: 'org.sonarqube'
    apply plugin: 'info.solidsoft.pitest'
    apply plugin: 'name.remal.check-updates'

    sourceCompatibility = 14

    dependencies {
        checkstyle 'com.puppycrawl.tools:checkstyle'

        annotationProcessor 'org.projectlombok:lombok'
        compileOnly 'org.projectlombok:lombok'

        testAnnotationProcessor 'org.projectlombok:lombok'
        testCompileOnly 'org.projectlombok:lombok'

        testImplementation 'io.cucumber:cucumber-java'
        testImplementation 'io.cucumber:cucumber-junit'
        testImplementation 'io.cucumber:cucumber-junit-platform-engine'
        testImplementation 'junit:junit'
        testImplementation 'org.junit.jupiter:junit-jupiter'
        testImplementation 'org.junit.jupiter:junit-jupiter-api'
        testImplementation 'org.junit.jupiter:junit-jupiter-engine'
        testImplementation 'org.junit.jupiter:junit-jupiter-params'
        testImplementation 'org.junit.platform:junit-platform-commons'
        testImplementation 'org.junit.platform:junit-platform-engine'
        testImplementation 'org.junit.vintage:junit-vintage-engine'
        testImplementation 'org.mockito:mockito-core'

        pitest 'org.pitest:pitest-junit5-plugin'
        pitest 'org.pitest:pitest-command-line:1.5.2'
    }

    checkDependencyUpdates {
        failIfUpdatesFound = true
    }

    checkstyleMain {
        configFile = file('../config/checkstyle/checkstyleMain.xml')
        maxWarnings = 0
    }

    checkstyleTest {
        configFile = file('../config/checkstyle/checkstyleTest.xml')
        maxWarnings = 0
    }

    jacocoTestCoverageVerification {
        violationRules {
            rule {
                limit {
                    minimum = 1.0
                }
            }
        }
    }
    check.dependsOn jacocoTestCoverageVerification

    jacocoTestReport {
        reports {
            xml.enabled true
            csv.enabled true
            html.enabled true
        }
    }
    test.finalizedBy jacocoTestReport

    pitest {
        targetClasses = ['com.nelkinda.template.*'] // TODO change
        pitestVersion = '1.5.2'
        timestampedReports = false
        testPlugin = 'junit5'
        outputFormats = ['XML', 'HTML']
        mutators = ['DEFAULTS', 'STRONGER', 'CONSTRUCTOR_CALLS', 'INLINE_CONSTS', 'REMOVE_CONDITIONALS', 'REMOVE_INCREMENTS']
        mutationThreshold = 100
        coverageThreshold = 100
    }

    pmd {
        ruleSets = []
        consoleOutput = true
        dependencies {
            pmd 'net.sourceforge.pmd:pmd'
            pmd 'net.sourceforge.pmd:pmd-core'
            pmd 'net.sourceforge.pmd:pmd-java'
        }
    }

    pmdMain {
        ruleSetFiles = files('../config/pmd/pmdMain.xml')
    }

    pmdTest {
        ruleSetFiles = files('../config/pmd/pmdTest.xml')
    }

    test {
        useJUnitPlatform()
    }
}

checkDependencyUpdates {
    failIfUpdatesFound = true
}

wrapper {
    gradleVersion '6.5.1'
    distributionType = Wrapper.DistributionType.ALL
}
