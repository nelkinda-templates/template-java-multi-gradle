buildscript {
    repositories {
        mavenCentral()
        maven {
            url 'https://plugins.gradle.org/m2/'
        }
    }
    dependencies {
        classpath 'name.remal:gradle-plugins:1.5.1'
        classpath 'org.sonarsource.scanner.gradle:sonarqube-gradle-plugin:3.3'
        classpath 'info.solidsoft.gradle.pitest:gradle-pitest-plugin:1.7.0'
    }
}

plugins {
    id 'io.spring.dependency-management' version '1.0.11.RELEASE'
}

allprojects {
    repositories {
        jcenter()
    }
}

subprojects {
    apply plugin: 'io.spring.dependency-management'

    version '0.0.1-SNAPSHOT'
    dependencyManagement {
        dependencies {
            dependency 'com.puppycrawl.tools:checkstyle:9.3'
            dependencySet(group: 'io.cucumber', version: '7.2.3') {
                entry 'cucumber-java'
                entry 'cucumber-junit-platform-engine'
            }
            dependencySet(group: 'net.sourceforge.pmd', version: '6.42.0') {
                entry 'pmd'
                entry 'pmd-core'
                entry 'pmd-java'
            }
            dependencySet(group: 'org.junit.jupiter', version: '5.8.2') {
                entry 'junit-jupiter'
                entry 'junit-jupiter-api'
                entry 'junit-jupiter-engine'
                entry 'junit-jupiter-params'
            }
            dependencySet(group: 'org.junit.platform', version: '1.8.2') {
                entry 'junit-platform-commons'
                entry 'junit-platform-engine'
                entry 'junit-platform-launcher'
                entry 'junit-platform-suite-api'
                entry 'junit-platform-suite-commons'
                entry 'junit-platform-suite-engine'
            }
            dependency 'org.mockito:mockito-core:4.5.1'
            dependency 'org.pitest:pitest-junit5-plugin:0.15'
            dependency 'org.pitest:pitest-command-line:1.7.4'
            dependency 'org.projectlombok:lombok:1.18.22'
        }
    }
}

configure(subprojects) {
    apply plugin: 'java'
    apply plugin: 'checkstyle'
    apply plugin: 'pmd'
    apply plugin: 'jacoco'
    apply plugin: 'name.remal.sonarlint'
    apply plugin: 'org.sonarqube'
    apply plugin: 'info.solidsoft.pitest'

    sourceCompatibility = 17

    dependencies {
        checkstyle 'com.puppycrawl.tools:checkstyle'

        annotationProcessor 'org.projectlombok:lombok'
        compileOnly 'org.projectlombok:lombok'

        testAnnotationProcessor 'org.projectlombok:lombok'
        testCompileOnly 'org.projectlombok:lombok'

        testImplementation 'io.cucumber:cucumber-java'
        testImplementation 'io.cucumber:cucumber-junit-platform-engine'
        testImplementation 'org.junit.jupiter:junit-jupiter'
        testImplementation 'org.junit.jupiter:junit-jupiter-api'
        testImplementation 'org.junit.jupiter:junit-jupiter-engine'
        testImplementation 'org.junit.jupiter:junit-jupiter-params'
        testImplementation 'org.junit.platform:junit-platform-commons'
        testImplementation 'org.junit.platform:junit-platform-engine'
        testImplementation 'org.junit.platform:junit-platform-suite-api'
        testImplementation 'org.junit.platform:junit-platform-suite-commons'
        testImplementation 'org.junit.platform:junit-platform-suite-engine'
        testImplementation 'org.mockito:mockito-core'

        pitest 'org.pitest:pitest-junit5-plugin'
        pitest 'org.pitest:pitest-command-line'
    }

    checkstyleMain {
        configFile = file('../config/checkstyle/checkstyleMain.xml')
        maxWarnings = 0
    }

    checkstyleTest {
        configFile = file('../config/checkstyle/checkstyleTest.xml')
        maxWarnings = 0
    }

    jacoco {
        toolVersion = "0.8.7"
    }

    jacocoTestCoverageVerification {
        violationRules {
            rule {
                limit {
                    minimum = 1.0
                }
            }
        }
    }
    check.dependsOn jacocoTestCoverageVerification

    jacocoTestReport {
        reports {
            xml.enabled true
            csv.enabled true
            html.enabled true
        }
    }
    test.finalizedBy jacocoTestReport

    pitest {
        targetClasses.set(['com.nelkinda.template.*']) // TODO change
        pitestVersion.set('1.7.3')
        timestampedReports.set(false)
        testPlugin.set('junit5')
        outputFormats.set(['XML', 'HTML'])
        mutators.set(['DEFAULTS', 'STRONGER', 'CONSTRUCTOR_CALLS', 'INLINE_CONSTS', 'REMOVE_CONDITIONALS', 'REMOVE_INCREMENTS'])
        mutationThreshold.set(100)
        coverageThreshold.set(100)
    }

    pmd {
        ruleSets = []
        consoleOutput = true
        dependencies {
            pmd 'net.sourceforge.pmd:pmd'
            pmd 'net.sourceforge.pmd:pmd-core'
            pmd 'net.sourceforge.pmd:pmd-java'
        }
    }

    pmdMain {
        ruleSetFiles = files('../config/pmd/pmdMain.xml')
    }

    pmdTest {
        ruleSetFiles = files('../config/pmd/pmdTest.xml')
    }

    test {
        useJUnitPlatform()
    }
}

wrapper {
    gradleVersion '7.4'
    distributionType = Wrapper.DistributionType.ALL
}
